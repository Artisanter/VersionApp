plugins {
    id "org.ajoberstar.grgit" version "3.1.1"
}
apply plugin: 'com.android.application'
apply plugin: 'org.ajoberstar.grgit'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    defaultConfig {
        applicationId "com.artisanter.versionapp"
        minSdkVersion 23
        targetSdkVersion 29

        def describe = "${grgit.open(currentDir: rootDir).describe(tags: true)}"
        def codes = describe.split("-|\\.")

        int major = codes[0].toInteger()
        int minor = codes[1].toInteger()
        int patch = codes[2].toInteger()

        versionCode patch + minor * 1000 + major * 10000000
        versionName "${major}.${minor}.${patch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        normal {
            dimension "version"
            applicationIdSuffix ".main"
            versionNameSuffix "-main"
        }
        green {
            dimension "version"
            applicationIdSuffix ".green"
            versionNameSuffix "-green"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

